plugins {
  id "me.champeau.gradle.jmh" version "0.3.0"
}

repositories {
    mavenCentral()
}

dependencies {
    //jmh 'commons-io:commons-io:2.4'
    compile 'org.dstadler:commons-dost:1.0.0.20'
    compile 'org.apache.commons:commons-email:1.3.3'

    compile 'com.fasterxml.jackson.core:jackson-core:2.7.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.7.4'

    // for IntelliJ JMH support
    compile 'org.openjdk.jmh:jmh-core:1.12'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.12'
}

jmh {
    // see https://github.com/melix/jmh-gradle-plugin
    jmhVersion = '1.12'
    iterations = 1
    humanOutputFile = project.file("${project.buildDir}/reports/jmh/human.txt")
    resultFormat = 'JSON'
}

// see https://github.com/melix/jmh-gradle-plugin/issues/6
tasks.getByName('jmhJar').doFirst() {duplicatesStrategy(DuplicatesStrategy.EXCLUDE)}

task wrapper(type: Wrapper) {
    gradleVersion = '2.12'
}

task adjustWrapperPropertiesFile << {
    ant.replaceregexp(match:'^#.*', replace:'', flags:'g', byline:true) {
        fileset(dir: project.projectDir, includes: 'gradle/wrapper/gradle-wrapper.properties')
    }
    new File(project.projectDir, 'gradle/wrapper/gradle-wrapper.properties').with { it.text = it.readLines().findAll { it }.sort().join('\n') }
    ant.fixcrlf(file: 'gradle/wrapper/gradle-wrapper.properties', eol: 'lf')
}
wrapper.finalizedBy adjustWrapperPropertiesFile


task publishResults(type:JavaExec) {
    description = 'Publishes results after the benchmark runs, stores results in a directory and sends out an email'

    main = 'org.apache.poi.benchmark.PublishResults'
    classpath = sourceSets.jmh.runtimeClasspath
}

task processResults(type:JavaExec) {
    description = 'Post-processes results to charts'

    main = 'org.apache.poi.benchmark.ProcessResults'
    classpath = sourceSets.jmh.runtimeClasspath
}
