plugins {
    // Release 0.4.0 and higher require Gradle 4.0
  id "me.champeau.gradle.jmh" version "0.4.6"
}

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.dstadler:commons-dost:1.0.0.25'
    compile 'org.apache.commons:commons-email:1.5'
    compile 'com.google.guava:guava:20.0'

    compile 'com.fasterxml.jackson.core:jackson-core:2.9.4'
    compile 'com.fasterxml.jackson.core:jackson-databind:2.9.4'

    // for IntelliJ JMH support
    compile 'org.openjdk.jmh:jmh-core:1.20'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.20'
}

jmh {
    // see https://github.com/melix/jmh-gradle-plugin
    jmhVersion = '1.20'
    iterations = 1
    humanOutputFile = project.file("${project.buildDir}/reports/jmh/human.txt")
    resultFormat = 'JSON'

    // newer JMH-Gradle plugin reported a duplicate file...
    duplicateClassesStrategy = 'warn'
}

// see https://github.com/melix/jmh-gradle-plugin/issues/6
tasks.getByName('jmhJar').doFirst() {duplicatesStrategy(DuplicatesStrategy.EXCLUDE)}

task wrapper(type: Wrapper) {
    gradleVersion = '4.6'
}

task adjustWrapperPropertiesFile {
    doLast {
        ant.replaceregexp(match: '^#.*', replace: '', flags: 'g', byline: true) {
            fileset(dir: project.projectDir, includes: 'gradle/wrapper/gradle-wrapper.properties')
        }
        new File(project.projectDir, 'gradle/wrapper/gradle-wrapper.properties').with {
            it.text = it.readLines().findAll { it }.sort().join('\n')
        }
        ant.fixcrlf(file: 'gradle/wrapper/gradle-wrapper.properties', eol: 'lf')
    }
}
wrapper.finalizedBy adjustWrapperPropertiesFile


task publishResults(type:JavaExec) {
    description = 'Publishes results after the benchmark runs, stores results in a directory and sends out an email'

    main = 'org.apache.poi.benchmark.PublishResults'
    classpath = sourceSets.jmh.runtimeClasspath
}

task processResults(type:JavaExec) {
    description = 'Post-processes results to charts'

    main = 'org.apache.poi.benchmark.ProcessResults'
    classpath = sourceSets.jmh.runtimeClasspath
}

processResults.mustRunAfter publishResults

