plugins {
  id "me.champeau.jmh" version "0.7.2"
}

repositories {
    mavenCentral()
}

configurations {
	all*.exclude group: 'javax.mail'	// avoid duplicate between com.sun.mail in commons-email and javax.mail in commons-test
}

dependencies {
    implementation 'org.dstadler:commons-dost:1.3.4'
	implementation 'commons-io:commons-io:2.17.0'
	implementation 'org.apache.commons:commons-lang3:3.17.0'
    implementation 'org.apache.commons:commons-email:1.5'
	implementation 'org.apache.commons:commons-exec:1.4.0'
    implementation 'com.google.guava:guava:33.3.1-jre'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.17.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.17.2'

    // for IntelliJ JMH support
    jmh 'org.openjdk.jmh:jmh-core:1.37'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.37'

    testImplementation('org.dstadler:commons-test:1.0.0.21')
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.11.0'
}

jmh {
    // see https://github.com/melix/jmh-gradle-plugin
    jmhVersion = '1.37'
    iterations = 1
    humanOutputFile = project.file("${project.buildDir}/reports/jmh/human.txt")
    resultFormat = 'JSON'

    // newer JMH-Gradle plugin reported a duplicate file...
    duplicateClassesStrategy = DuplicatesStrategy.WARN
}

// see https://github.com/melix/jmh-gradle-plugin/issues/6
tasks.getByName('jmhJar').doFirst() {duplicatesStrategy(DuplicatesStrategy.EXCLUDE)}

wrapper {
    gradleVersion = '8.5'
}

tasks.register('publishResults', JavaExec) {
	description = 'Publishes results after the benchmark runs, stores results in a directory and sends out an email'

	mainClass = 'org.apache.poi.benchmark.PublishResults'
	classpath = sourceSets.jmh.runtimeClasspath
}

tasks.register('processResults', JavaExec) {
	description = 'Post-processes results to charts'

	mainClass = 'org.apache.poi.benchmark.ProcessResults'
	classpath = sourceSets.jmh.runtimeClasspath
}

processResults.mustRunAfter publishResults

// prevent build-failure due to new "duplicationStrategy" in Gradle 7.x
// see https://docs.gradle.org/7.6/dsl/org.gradle.api.tasks.Copy.html#org.gradle.api.tasks.Copy:duplicatesStrategy
// and https://github.com/gradle/gradle/issues/17236
tasks.withType(Copy.class).configureEach {
    duplicatesStrategy = DuplicatesStrategy.WARN
}
