plugins {
  id "me.champeau.jmh" version "0.7.2"
}

repositories {
    mavenCentral()
}

configurations {
	all*.exclude group: 'javax.mail'	// avoid duplicate between com.sun.mail in commons-email and javax.mail in commons-test
}

dependencies {
    implementation 'org.dstadler:commons-dost:1.4.4'
	implementation 'commons-io:commons-io:2.21.0'
	implementation 'org.apache.commons:commons-lang3:3.20.0'
    implementation 'org.apache.commons:commons-email:1.5'
	implementation 'org.apache.commons:commons-exec:1.6.0'
    implementation 'com.google.guava:guava:33.5.0-jre'

    implementation 'com.fasterxml.jackson.core:jackson-core:2.20.1'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.20.1'

    // for IntelliJ JMH support
    jmh 'org.openjdk.jmh:jmh-core:1.37'
    jmh 'org.openjdk.jmh:jmh-generator-annprocess:1.37'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:6.0.1'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:6.0.1'

    testImplementation('org.dstadler:commons-test:1.0.0.22')
}

jmh {
    // see https://github.com/melix/jmh-gradle-plugin
    jmhVersion = '1.37'
    iterations = 1
    humanOutputFile = project.file("${project.buildDir}/reports/jmh/human.txt")
    resultFormat = 'JSON'

    // newer JMH-Gradle plugin reported a duplicate file...
    duplicateClassesStrategy = DuplicatesStrategy.WARN
}

test {
    useJUnitPlatform()
}

// see https://github.com/melix/jmh-gradle-plugin/issues/6
tasks.getByName('jmhJar').doFirst() {duplicatesStrategy(DuplicatesStrategy.EXCLUDE)}

wrapper {
    gradleVersion = '8.14.3'
}

tasks.register('publishResults', JavaExec) {
	description = 'Publishes results after the benchmark runs, stores results in a directory and sends out an email'

	mainClass = 'org.apache.poi.benchmark.PublishResults'
	classpath = sourceSets.jmh.runtimeClasspath
}

tasks.register('processResults', JavaExec) {
	description = 'Post-processes results to charts'

	mainClass = 'org.apache.poi.benchmark.ProcessResults'
	classpath = sourceSets.jmh.runtimeClasspath
}

processResults.mustRunAfter publishResults

// prevent build-failure due to new "duplicationStrategy" in Gradle 7.x
// see https://docs.gradle.org/7.6/dsl/org.gradle.api.tasks.Copy.html#org.gradle.api.tasks.Copy:duplicatesStrategy
// and https://github.com/gradle/gradle/issues/17236
tasks.withType(Copy.class).configureEach {
    duplicatesStrategy = DuplicatesStrategy.WARN
}
